# -*- mode:sh -*-

###
# See if we can use colors.
autoload zsh/terminfo

# set colors for use in prompts
if autoload colors && colors 2>/dev/null ; then
    for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
	    eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
	    eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
	    (( count = $count + 1 ))
    done
    PR_NO_COLOUR="%{$terminfo[sgr0]%}"
else
    PR_BLUE=$'%{\e[1;34m%}'
    PR_RED=$'%{\e[1;31m%}'
    PR_GREEN=$'%{\e[1;32m%}'
    PR_CYAN=$'%{\e[1;36m%}'
    PR_WHITE=$'%{\e[1;37m%}'
    PR_MAGENTA=$'%{\e[1;35m%}'
    PR_YELLOW=$'%{\e[1;33m%}'
    PR_NO_COLOR=$'%{\e[0m%}'
fi

###
# See if we can use extended characters to look nicer.
typeset -A altchar
set -A altchar ${(s..)terminfo[acsc]}
PR_SET_CHARSET="%{$terminfo[enacs]%}"
PR_SHIFT_IN="%{$terminfo[smacs]%}"
PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
PR_HBAR=${altchar[q]:--}
PR_ULCORNER=${altchar[l]:--}
PR_LLCORNER=${altchar[m]:--}
PR_LRCORNER=${altchar[j]:--}
PR_URCORNER=${altchar[k]:--}

case $TERM in
	xterm*|rxvt*)
	    PR_TITLEBAR=$'%{\e]0;%(!.-=*[ROOT]*=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\a%}'
	    ;;
	screen)
	    PR_TITLEBAR=$'%{\e_screen \005 (\005t) | %(!.-=[ROOT]=- | .)%n@%m:%~ | ${COLUMNS}x${LINES} | %y\e\\%}'
	    ;;
	*)
	    PR_TITLEBAR=''
	    ;;
esac

###
# Decide the color of the hostname
if [[ "$HOSTNAME" = 'gkar' ||  "$HOSTNAME" = 'delenn' ]]; then
	PR_HCOLOR="${PR_GREEN}"
else
	case $DOMAIN in
		"debconf.org")
			PR_HCOLOR="${PR_CYAN}"
			;;
		"debian.org")
			PR_HCOLOR="${PR_WHITE}"
			;;
		*)
			PR_HCOLOR="${PR_RED}"
			;;
	esac
fi

###
# Finally, the prompt.

PROMPT='$PR_SET_CHARSET$PR_STITLE${(e)PR_TITLEBAR}\
$PR_CYAN$PR_SHIFT_IN$PR_ULCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_GREEN%(!.%SROOT%s.%n)$PR_GREEN@$PR_HCOLOR%m$PR_GREEN:%l\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_HBAR${(e)PR_APM}\
$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_HBAR$PR_HBAR${(e)PR_FILLBAR}$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
$PR_MAGENTA%$PR_PWDLEN<...<%~%<<\
$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_URCORNER$PR_SHIFT_OUT\

$PR_CYAN$PR_SHIFT_IN$PR_LLCORNER$PR_BLUE$PR_HBAR$PR_SHIFT_OUT(\
%(?..$PR_LIGHT_RED%?$PR_BLUE:)\
$PR_YELLOW%D{%H:%M}$PR_BLUE)\
$PR_WHITE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT${debian_chroot:+($debian_chroot)}${PR_NO_COLOR}\
$PR_SHIFT_IN$PR_HBAR $PR_SHIFT_OUT${vcs_info_msg_0_}\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_NO_COLOUR| '

RPROMPT=' |$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_BLUE$PR_HBAR$PR_SHIFT_OUT\
($PR_YELLOW%D{%a,%b%d}$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_CYAN$PR_LRCORNER$PR_SHIFT_OUT$PR_NO_COLOUR'

PS2='$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_BLUE$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT(\
$PR_LIGHT_GREEN%_$PR_BLUE)$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT\
$PR_CYAN$PR_SHIFT_IN$PR_HBAR$PR_SHIFT_OUT$PR_NO_COLOUR '
